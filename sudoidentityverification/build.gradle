apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'com.amazonaws.appsync'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "2.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        animationsDisabled = true

        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }

            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
        androidTest {
            resources.srcDirs += ['/var/anonyome_key', '../identity-system-test-config']
            assets.srcDirs += ['../identity-system-test-config']
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8'

    // Sudo Platform SDKs.
    implementation 'com.sudoplatform:sudouser:[8.0,)'
    implementation 'com.sudoplatform:sudokeymanager:[3.4,)'
    implementation 'com.sudoplatform:sudologging:[1.0,)'
    implementation 'com.sudoplatform:sudoconfigmanager:[1.2.2,)'
    implementation 'com.sudoplatform:sudoapiclient:[1.4.0,)'

    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.4.4'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.7'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.7'
    testImplementation 'org.json:json:20200518'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'com.google.android.gms:play-services-safetynet:17.0.0'
    androidTestImplementation 'com.google.android.gms:play-services-base:17.3.0'
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    classDirectories.from = fileTree(
            dir: "${buildDir}/tmp/kotlin-classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )

    sourceDirectories.from = files("${buildDir.parent}/sudoidentityverification/src/main/java")
    executionData.from = files("${buildDir}/jacoco/testDebugUnitTest.exec")

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

apply plugin: 'maven'

def props = new Properties()
file("../secret.properties").withInputStream { props.load(it) }

ext {
    PUBLISH_GROUP_ID = 'com.sudoplatform'
    PUBLISH_ARTIFACT_ID = 'sudoidentityverification'
    PUBLISH_DESCRIPTION = 'Secure ID Verification SDK for the Sudo Platform by Anonyome Labs.'
    PUBLISH_PROJECT_URL = 'https://github.com/sudoplatform/sudo-secure-id-verification-android'
    PUBLISH_PROJECT_SCM = 'scm:git:github.com/sudoplatform/sudo-secure-id-verification-android.git'
    if (project.hasProperty('tag')) {
        PUBLISH_VERSION = "$tag"
    } else {
        PUBLISH_VERSION = "0.0.0"
    }
}

if (project.hasProperty("tag")) {
    apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"
} else {
    //// Snapshot publishing
    apply from: "${rootProject.projectDir}/publish-snapshot-android.gradle"
}

